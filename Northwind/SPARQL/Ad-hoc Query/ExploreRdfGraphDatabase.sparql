

# Explore a Graph Database


# The following prefixes need to be added to the database or 
# to the beggining of the queries that require them.
# PREFIX     : <http://www.mysparql.com/resource/northwind/>
# PREFIX  rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
# PREFIX  xsd: <http://www.w3.org/2001/XMLSchema#>
# PREFIX foaf: <http://xmlns.com/foaf/0.1/>
# PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>


# Total triple count
SELECT
    (COUNT(?s) as ?numTriples)
WHERE {
    ?s ?p ?o .
}


# Selecting all triples
SELECT * {?s ?p ?o}


# Selecting all properties of a class, e.g. Order
SELECT DISTINCT
  ?domain ?prop ?range
WHERE {
    ?subject ?prop ?object .
    ?subject a ?domain .
    OPTIONAL {
        ?object a ?oClass .
    }
    BIND(IF(BOUND(?oClass), ?oClass, DATATYPE(?object)) AS ?range)
    FILTER (?prop != rdf:type && ?prop != rdfs:domain && ?prop != rdfs:range 
    && ?domain = :Order
    )
}


# Constructing a graph with the properties of a class, e.g. Order
CONSTRUCT {
    ?domain ?prop ?range
}
WHERE {
    ?subject ?prop ?object .
    ?subject a ?domain .
    OPTIONAL {
        ?object a ?oClass .
    }
    BIND(IF(BOUND(?oClass), ?oClass, DATATYPE(?object)) as ?range)
    FILTER (?prop != rdf:type && ?prop != rdfs:domain && ?prop != rdfs:range 
    && ?domain = :Order
    )
}


# Constructing a graph with the properties of specific classes.
CONSTRUCT {
    ?domain ?prop ?range
}
WHERE {
    ?subject ?prop ?object .
    ?subject a ?domain .
    OPTIONAL {
        ?object a ?oClass .
    }
    BIND(IF(BOUND(?oClass), ?oClass, DATATYPE(?object)) as ?range)
    FILTER (?prop != rdf:type && ?prop != rdfs:domain && ?prop != rdfs:range 
    && ?domain IN ( :Order, :Customer, :OrderDetail, :Product )
    )
}


# Counting predicates (relationships)
SELECT
    ?predicate (COUNT(?predicate) as ?predicateCount)
WHERE {
    ?subject ?predicate ?object .
}
GROUP BY
    ?predicate
ORDER BY
    DESC(?predicateCount)


# Counting class instances
SELECT
    ?class (COUNT(?subject) as ?classCount)
WHERE {
    ?subject rdf:type ?class .
    FILTER (?class != rdfs:Class && ?class != rdf:Property)
}
GROUP BY
    ?class
ORDER BY
    DESC(?classCount)


# Counting triples in graphs
SELECT
     ?g (COUNT(*) AS ?Count)
WHERE {
     {
          GRAPH ?g {?s ?p ?o}
     } UNION {
          ?s ?p ?o
          BIND("default" AS ?g)
     }
}
GROUP BY ?g
ORDER BY DESC(?Count)


# Constructing a graph
CONSTRUCT {
    ?domain ?prop ?range
}
WHERE {
    ?subject ?prop ?object .
    ?subject a ?domain .
    OPTIONAL {
        ?object a ?oClass .
    }
    bind(if(BOUND(?oClass), ?oClass, DATATYPE(?object)) as ?range)
    FILTER (?prop != rdf:type && ?prop != rdfs:domain && ?prop != rdfs:range)
}


# Returning all the outgoing edges of the node (execute one statement at a time)
DESCRIBE :orderDetail-10248-72
DESCRIBE :order-10248
DESCRIBE :product-1


# Search everywhere query
SELECT
     ?g ?s ?p ?o
WHERE {
{
     GRAPH ?g {?s ?p ?o}
     } UNION {
     ?s ?p ?o
     BIND("default" AS ?g)
}
FILTER (
     (CONTAINS (STR(?s), ?searchString)) ||
     (CONTAINS (STR(?p), ?searchString)) ||
     (CONTAINS (STR(?o), ?searchString))
)
BIND("category-3" AS ?searchString)
}
ORDER BY
     ?g ?s

